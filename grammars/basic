S = RE_BRANCH ("|" RE_BRANCH)*
RE_BRANCH = SIMPLE_RE+
SIMPLE_RE =  (ONE_CHAR_RE | GROUP) DUPL_SYMBOL?
ONE_CHAR_RE = ORD_CHAR | ANY_CHAR | BRACKET_EXPRESSION
GROUP = "(" S ")"
DUPL_SYMBOL = "*" | "+"
ANY_CHAR = "."
ORD_CHAR=#"[^\^\.\[\$\(\)\|\*\+\?\{\\]"
SPEC_CHAR=#"[\^\.\[\$\(\)\|\*\+\?\{\\]"

(* Bracket expressions. Only supporting single chars and ranges *)

BRACKET_EXPRESSION= <"["> (MATCHING_LIST | NON_MATCHING_LIST) <"]">
NON_MATCHING_LIST= "^" MATCHING_LIST
MATCHING_LIST=  (RANGE_EXPRESSION | SINGLE_EXPRESSION)+ TRAILING_HYPHEN?
RANGE_EXPRESSION= SINGLE_EXPRESSION "-" SINGLE_EXPRESSION
SINGLE_EXPRESSION=#"[^\]-]|\[\]]" (* Basically allowing some invalid input like [^]  *)
TRAILING_HYPHEN="-"